	
/*Importing Excel Files TO SAS*/

%let location= /folders/myfolders/sasuser.v94/data/EAG Report/;

%macro Import(file_name,ds_name,db_name);
   proc import
   datafile="&location&file_name"
   out=&ds_name
   dbms=&db_name
   replace;
   run;
%mend;

%import(EAGBR.xls,Brok,xls);
%import(EAGTN.xls,Turn,xls);

/*Brokerage Report*/

title"EAG Brokerage Report (In Lakhs)";

 PROC REPORT DATA=WORK.BROK LS=132 PS=60  SPLIT="/" CENTER ;
 COLUMN  PRODUCT 
 ("EAG+PCG" EAG_EQUITY_REVENUE PCG_EQUITY_REVENUE RELIGARE_REVENUE EAG_P PCG_P)
 ("NRI" NRI_EQUITY_REVENUE  NRI_IDIRECT_REVENUE NRI_P)
 ("Total Revenue" TEAM_WISE_TOTAL TOTAL_EQUITY_REVENUE Total_P);
  
 DEFINE  PRODUCT / display FORMAT= $25. WIDTH=25    SPACING=2   LEFT "PRODUCT" ;
 
 DEFINE  EAG_EQUITY_REVENUE / SUM FORMAT= BEST20. WIDTH=20    SPACING=2   RIGHT "EAG" ;
 DEFINE  PCG_EQUITY_REVENUE / SUM FORMAT= BEST20. WIDTH=20    SPACING=2   RIGHT "PCG" ;
 DEFINE  RELIGARE_REVENUE / SUM FORMAT= BEST20. WIDTH=20    SPACING=2   RIGHT "TOTAL" ;
 DEFINE  EAG_P / COMPUTED FORMAT= PERCENT8.2 WIDTH=20    SPACING=2   RIGHT "EAG% of/TOTAL/Revenue" ;
 DEFINE  PCG_P / COMPUTED FORMAT= PERCENT8.2 WIDTH=20    SPACING=2   RIGHT "PCG% of/TOTAL/Revenue" ;

 DEFINE  NRI_EQUITY_REVENUE / SUM FORMAT= BEST20. WIDTH=20    SPACING=2   RIGHT "NRI" ;
 DEFINE  NRI_IDIRECT_REVENUE / SUM FORMAT= BEST20. WIDTH=20    SPACING=2   RIGHT "TOTAL" ;
 DEFINE  NRI_P / COMPUTED FORMAT= PERCENT8.2 WIDTH=20    SPACING=2   RIGHT "NRI% of/TOTAL/Revenue" ;
	 
 DEFINE  TEAM_WISE_TOTAL / SUM FORMAT= BEST19. WIDTH=19    SPACING=2   RIGHT "Team Wise/Total" ;
 DEFINE  TOTAL_EQUITY_REVENUE / SUM FORMAT= BEST20. WIDTH=20    SPACING=2   RIGHT "TOTAL/Revenue" ;
 DEFINE  TOTAL_P / COMPUTED FORMAT= PERCENT8.2 WIDTH=20    SPACING=2   RIGHT "% of TOTAL/Revenue" ;

 Compute EAG_P;
 EAG_P=(EAG_EQUITY_REVENUE.sum/RELIGARE_REVENUE.sum);
 endcomp;

 Compute PCG_P;
 PCG_P=(PCG_EQUITY_REVENUE.sum/RELIGARE_REVENUE.sum);
 endcomp;

 Compute NRI_P;
 NRI_P=(NRI_EQUITY_REVENUE.sum/NRI_IDIRECT_REVENUE.sum);
 endcomp;

 Compute Total_P;
 TOTAL_P=(Team_Wise_Total.sum/Total_Equity_REVENUE.sum);
 endcomp;
 
 compute after;
 Product ="Total";
 endcomp;
 
 Rbreak after/summarize style={background=lightgrey};
 
 RUN;
 
 /*Turnover Report*/
options missing=0;
title"EAG Turnover Report (In Crores)";

 PROC REPORT DATA=WORK.TURN LS=132 PS=60  SPLIT="/" CENTER ;
 COLUMN  PRODUCTS 
 ("EAG+PCG" EAG_TURNOVER PCG_TURNOVER RELIGARE_TURNOVER EAG_P PCG_P)
 ("NRI" NRI_TURNOVER NRI_IDIRECT_TURNOVER NRI_P)
 ("Total Revenue" TEAM_WISE_TURNOVER RELIGARE_DIRECT_TURNOVER Total_P);

 DEFINE  PRODUCTS / Display FORMAT= $36. WIDTH=36    SPACING=2   LEFT "PRODUCT" ;
  
 DEFINE  EAG_TURNOVER / SUM FORMAT= BEST17. WIDTH=17    SPACING=2   RIGHT "EAG" ;
 DEFINE  PCG_TURNOVER / SUM FORMAT= BEST17. WIDTH=17    SPACING=2   RIGHT "PCG" ;
 DEFINE  RELIGARE_TURNOVER / SUM FORMAT= BEST20. WIDTH=20    SPACING=2   RIGHT "TOTAL" ;
 DEFINE  EAG_P / COMPUTED FORMAT= PERCENT8.2 WIDTH=20    SPACING=2   RIGHT "EAG% of/TOTAL/Revenue" ;
 DEFINE  PCG_P / COMPUTED FORMAT= PERCENT8.2 WIDTH=20    SPACING=2   RIGHT "PCG% of/TOTAL/Revenue" ;

 DEFINE  NRI_TURNOVER / SUM FORMAT= BEST16. WIDTH=16    SPACING=2   RIGHT "NRI" ;
 DEFINE  NRI_IDIRECT_TURNOVER / SUM FORMAT= BEST20. WIDTH=20    SPACING=2   RIGHT "TOTAL" ;
 DEFINE  NRI_P / COMPUTED FORMAT= PERCENT8.2 WIDTH=20    SPACING=2   RIGHT "NRI% of/TOTAL/Revenue" ;

 DEFINE  TEAM_WISE_TURNOVER / SUM FORMAT= BEST20. WIDTH=20    SPACING=2   RIGHT "Team Wise/Total" ;
 DEFINE  RELIGARE_DIRECT_TURNOVER / SUM FORMAT= BEST20. WIDTH=20    SPACING=2   RIGHT "TOTAL/Turnover" ;
 DEFINE  TOTAL_P / COMPUTED FORMAT= PERCENT8.2 WIDTH=20    SPACING=2   RIGHT "% of TOTAL/Turnover" ;

 Compute EAG_P;
 EAG_P=(EAG_TURNOVER.sum/RELIGARE_TURNOVER.sum);
 endcomp;

 Compute PCG_P;
 PCG_P=(PCG_TURNOVER.sum/RELIGARE_TURNOVER.sum);
 endcomp;

 Compute NRI_P;
 NRI_P=(NRI_TURNOVER.sum/NRI_IDIRECT_TURNOVER.sum);
 endcomp;

 Compute Total_P;
 TOTAL_P=(Team_Wise_Turnover.sum/RELIGARE_DIRECT_TURNOVER.sum);
 endcomp;

 compute after;
 Products ="Total";
 endcomp;
 
 Rbreak after/summarize style={background=lightgrey};

 RUN;